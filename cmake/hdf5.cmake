set(HDF5_NAME hdf5)
set(HDF5_URL ${PROJECT_SOURCE_DIR}/files)
set(HDF5_GZ hdf5-1.8.15-patch1.tar.gz)
set(HDF5_MD5 "4467c25ed9c0b126b194a4d9d66c29ac")

message(STATUS "Building ${HDF5_NAME}")
ExternalProject_Add(${HDF5_NAME}
  DEPENDS ${ZLIB_PACKAGE_NAME} ${SZIP_PACKAGE_NAME}
  URL ${HDF5_URL}/${HDF5_GZ}
  URL_MD5 ${HDF5_MD5}
  PREFIX ${HDF5_NAME}
  INSTALL_DIR ${HDF5_NAME}/install
  UPDATE_COMMAND ""
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
    -DCMAKE_CXX_COMPILER:FILEPATH=${CMAKE_CXX_COMPILER}
    -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
    -DCMAKE_C_COMPILER:FILEPATH=${CMAKE_C_COMPILER}
    -DCMAKE_C_FLAGS:STRING=${CMAKE_C_FLAGS}
    -DBUILD_SHARED_LIBS:BOOL=${BUILD_SHARED_LIBS}
    -DBUILD_STATIC_EXECS:BOOL=OFF
    -DBUILD_TESTING:BOOL=OFF
    -DHDF5_BUILD_HL_LIB:BOOL=ON
    -DHDF5_BUILD_CPP_LIB:BOOL=ON
    -DHDF5_BUILD_FORTRAN:BOOL=OFF
    -DHDF5_BUILD_TOOLS:BOOL=OFF
    -DHDF5_PACKAGE_EXTLIBS:BOOL=ON
    -DHDF5_ENABLE_Z_LIB_SUPPORT:BOOL=ON
    -DZLIB_USE_EXTERNAL:STRING=ON
    -DZLIB_FOUND:STRING=ON
    -DZLIB_PACKAGE_NAME:STRING=${ZLIB_PACKAGE_NAME}
    -DZLIB_INCLUDE_DIR:PATH=${ZLIB_INCLUDE_DIR}
    -DZLIB_LIBRARIES:FILEPATH=${ZLIB_LIBRARIES}
    -DHDF5_ENABLE_SZIP_SUPPORT:BOOL=ON
    -DHDF5_ENABLE_SZIP_ENCODING:BOOL=ON
    -DSZIP_USE_EXTERNAL:STRING=ON
    -DSZIP_FOUND:STRING=ON
    -DSZIP_PACKAGE_NAME:STRING=${SZIP_PACKAGE_NAME}
    -DSZIP_INCLUDE_DIR:PATH=${SZIP_INCLUDE_DIR}
    -DSZIP_LIBRARIES:FILEPATH=${SZIP_LIBRARIES}
    -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
   LOG_BUILD 1
)
ExternalProject_get_property(${HDF5_NAME} INSTALL_DIR)

set(HDF5_INCLUDE_DIR ${INSTALL_DIR}/include)
if (CMAKE_BUILD_TYPE MATCHES Debug)
  set(HDF5_LIBRARIES     ${INSTALL_DIR}/lib/${CMAKE_LIBRARY_PREFIX}hdf5_debug${CMAKE_LIBRARY_SUFFIX})
  set(HDF5_HL_LIBRARIES  ${INSTALL_DIR}/lib/${CMAKE_LIBRARY_PREFIX}hdf5_hl_debug${CMAKE_LIBRARY_SUFFIX})
else()
  set(HDF5_LIBRARIES     ${INSTALL_DIR}/lib/${CMAKE_LIBRARY_PREFIX}hdf5${CMAKE_LIBRARY_SUFFIX})
  set(HDF5_HL_LIBRARIES  ${INSTALL_DIR}/lib/${CMAKE_LIBRARY_PREFIX}hdf5_hl${CMAKE_LIBRARY_SUFFIX})
endif()

install(DIRECTORY ${INSTALL_DIR}/ DESTINATION ${CMAKE_INSTALL_PREFIX} USE_SOURCE_PERMISSIONS)
